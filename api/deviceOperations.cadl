import "@cadl-lang/rest";

import "./models/device.cadl";
import "./models/deviceRoutes.cadl";

using Cadl.Http;
using Cadl.Rest;

@service({
    title: "Tailscale API",
    version: "2023-01-01",
})
@server("https://api.tailscale.com/api/v2", "Tailscale API")
@doc("This namespace contains operations that can be performed on a Tailscale-connected device.")
@route("/device")
namespace DeviceEndpoints {
    model CommonParameters {
        @path
        @segment("device")
        id: string;
    }

    @error
    model Error {
        message: string;
    }

    @get op getDevice(...CommonParameters): Device;

    @delete op deleteDevice(...CommonParameters): Response<200> | Error;

    @route("/{id}/authorized")
    @post
    op authorizeDevice(
        @path id: string,
        @body setAuthorized: SetAuthorized
    ): Response<200>;

    @route("/{id}/tags")
    @post
    op setTags(@path id: string, @body setTags: SetTags): Response<200>;

    @route("/{id}/routes")
    @get
    op getDeviceRoutes(@path id: string): DeviceRoutes;

    @route("/{id}/routes")
    @post
    op setRoutes(
        @path id: string,
        @body setRoutes: SetDeviceRoutes
    ): DeviceRoutes;

    @route("/{id}/key")
    @post
    op setKeyExpiry(
        @path id: string,
        @body setKeyExpiry: SetKeyExpiry
    ): Response<200>;
}
